//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'idea'
    alias(libs.plugins.ben.manes) apply false
    alias(libs.plugins.kotlin) apply false
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency.management) apply false

}

version '1.0-SNAPSHOT'

wrapper { distributionType = Wrapper.DistributionType.ALL; gradleVersion = '7.2' }

apply from: "$rootDir/gradle/util.gradle"

subprojects {
    if (name.contains('backend')) {

        apply plugin: 'kotlin'
        apply plugin: 'com.github.ben-manes.versions'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'

        repositories {
            mavenLocal()
            mavenCentral()
        }

        java.sourceCompatibility = JavaVersion.VERSION_11

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                freeCompilerArgs = ['-Xjsr305=strict']
                jvmTarget = JavaVersion.VERSION_11.majorVersion
            }
        }

        tasks.withType(Test) { useJUnitPlatform() }

        tasks.test {
            systemProperties["kotest.listener.spring.ignore.warning"] = true
            systemProperties["gradle.build.dir"] = buildDir
        }

        dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-reflect'
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'

            implementation(platform(libs.testcontainers))
        }
    }
}